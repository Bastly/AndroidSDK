apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile ('org.zeromq:jeromq:0.3.4@jar') {transitive=true}
    compile ('com.google.code.gson:gson:1.7.2@jar') {transitive=true}
}

version = "1.0.2"
group = "com.bastly"

task createPom << {
    pom {
        project {
            groupId 'com.bastly'
            artifactId 'androidSDK'
            version '1.0.0'
        }
    }.writeTo("pom.xml")
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'AndroidSDK'
                description = 'Bastly SDK library for android'
                url 'https://github.com/Bastly/AndroidSDK'

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'goofyahead'
                        name 'Alex Vidal'
                        email 'MY_EMAIL'
                    }
                }
                scm {
                    connection 'git@github.com:Bastly/AndroidSDK.git'
                    developerConnection 'git@github.com:Bastly/AndroidSDK.git'
                    url 'https://github.com/Bastly/AndroidSDK'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    into('libs') {
        from configurations.compile
    }
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = "bastly"
        repo = "releases"
        name = "AndroidSDK"
        websiteUrl = "http://bastly.com"
        vcsUrl = "git@github.com:Bastly/AndroidSDK.git"
        licenses = ["AGPLv3"]
        publish = true
    }
}